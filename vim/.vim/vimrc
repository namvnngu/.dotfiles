"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" OPTIONS                                                                     "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let mapleader = ' '

syntax on
filetype plugin indent on

set autoindent
set autoread
set breakindent
set clipboard+=unnamedplus,unnamed
set colorcolumn=80
set completeopt=menu,menuone,popup,noinsert,noselect
set copyindent
set expandtab
set foldlevel=99
set foldlevelstart=99
set grepformat=%f:%l:%c:%m
set grepprg=rg\ --vimgrep
set hlsearch
set ignorecase
set incsearch
set infercase
set laststatus=2
set linebreak
set nobackup
set noswapfile
set noundofile
set nowrap
set nowritebackup
set number
set omnifunc=syntaxcomplete#Complete
set preserveindent
set relativenumber
set scrolloff=8
set shiftround
set shiftwidth=2
set showtabline=0
set sidescrolloff=8
set signcolumn=no
set smartcase
set smartindent
set smoothscroll
set splitbelow
set splitright
set statusline=%<%F\ %h%m%r%=%-14.(%l,%c%V%)\ %P
set tabstop=2
set timeoutlen=500
set undolevels=10000
set updatetime=50
set virtualedit=block
set wildmenu
set wildmode=longest:full,full
set wildoptions=fuzzy,pum,tagfile
set winminwidth=5

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" MAPPINGS                                                                    "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Move to window
nnoremap <leader>h :wincmd h<CR>
nnoremap <leader>j :wincmd j<CR>
nnoremap <leader>k :wincmd k<CR>
nnoremap <leader>l :wincmd l<CR>

" Resize window
nnoremap <Up> :resize +2<CR>
nnoremap <Down> :resize -2<CR>
nnoremap <Left> :vertical resize -2<CR>
nnoremap <Right> :vertical resize +2<CR>

" Yanking
nnoremap <leader>ya :%y+<CR>
nnoremap <leader>y "+y
vnoremap <leader>y "+y
nnoremap <leader>Y "+y$
nnoremap Y y$

" Buffers
nnoremap <leader>bn :bnext<CR>
nnoremap <leader>bp :bprevious<CR>
nnoremap <leader>bd :bdelete<CR>
nnoremap <leader>ba :bufdo bd<CR>
nnoremap <leader>be :%bd\|e#<CR>

" Better indenting
vnoremap < <gv
vnoremap > >gv

"  Move lines
nnoremap <leader>md :m .+1<CR>==
nnoremap <leader>mu :m .-2<CR>==
vnoremap J :m '>+1<CR>gv=gv
vnoremap K :m '<-2<CR>gv=gv

" Clear search with <Esc>
nnoremap <Esc> :noh<CR>

" Keeping it centered and stable
nnoremap J mzJ`z
nnoremap n nzzzv
nnoremap N Nzzzv
nnoremap <C-D> <C-D>zz
nnoremap <C-U> <C-U>zz

" Quickfix list
nnoremap <C-J> :cnext<CR>zz
nnoremap <C-K> :cprev<CR>zz

" Location list
nnoremap <leader>J :lnext<CR>zz
nnoremap <leader>K :lprev<CR>zz

" Toggle spelling checker
map <F4> :setlocal spell! spelllang=en_us<CR>

" Replace currently selected text with default register without yanking it
xnoremap <leader>p "_dP

" Delete without yanking
nnoremap <leader>d "_d
vnoremap <leader>d "_d

" Cancel
inoremap <C-C> <Esc>

" Replace
nnoremap <leader>s :%s/\<<C-R><C-W>\>/<C-R><C-W>/gI<Left><Left><Left>

" Make file executable
nnoremap <silent> <leader>x :!chmod +x %<CR>

" Copy the current file's path
nnoremap yp :exec setreg('+', expand('%:p'))<CR>

" Switch editing file
nnoremap <leader>e :e <C-R>%

" Open a file name under cursor in a new vertical split
nnoremap <leader><C-W>F :vertical wincmd F<CR>

" Split explorer
nnoremap - :Ex<CR>

" Switch between C source and header file
nnoremap <F5> :e %:p:s,.h$,.X123X,:s,.c$,.h,:s,.X123X$,.c,<CR>

" Fix the indentation of an entire file
nnoremap <leader>F gg=G''

" Set undo points when deleting text in insert mode
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ABBREVIATIONS                                                               "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

ia <expr> me:: strftime('Author: Nam Nguyen <vnngucs@outlook.com><CR>Date: %d %b %Y<CR>License: MIT')

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" AUTOCOMMANDS                                                                "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

augroup trim_white_space
  autocmd!
  autocmd BufWritePre * call s:TrimWhiteSpace()
augroup END
function s:TrimWhiteSpace()
  let l:save = winsaveview()
  keepjumps keeppatterns silent! %s/\s\+$//e
  keepjumps keeppatterns silent! %s/\%^\n\+//
  keepjumps keeppatterns silent! %s/\(\n\n\)\n\+/\1/
  keepjumps keeppatterns silent! %s/\($\n\s*\)\+\%$//
  call winrestview(l:save)
endfunction

augroup lang_tags
  autocmd!
  autocmd BufEnter,BufNew * set tags='./tags,tags'
augroup END
augroup c_tags
  autocmd!
  autocmd BufEnter,BufNew *.c,*.h call s:SetTags('c')
augroup END
function s:SetTags(lang)
  let l:files = globpath('~/.tags/' . a:lang, '*', 0, 1)
  if len(l:files) == 0
    let &tags = './tags,tags'
  else
    let &tags = './tags,tags,' . join(l:files, ',')
  endif
endfunction

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" COMMANDS                                                                "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

command! Ctags !ctags -R .

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" PLUGINS                                                                     "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Plugin Manager

let s:plugin_dir = '~/.vim/pack/plugins/start'

function s:CountDirectories(path)
  return len(filter(globpath(a:path, '*', 0, 1), 'isdirectory(v:val)'))
endfunction

function s:InstallPlugin(plugin_source_url, plugin_name)
  let l:plugin_dest_dir = expand(s:plugin_dir . '/' . a:plugin_name)

  if !isdirectory(l:plugin_dest_dir)
    echo 'Installing ' . a:plugin_name . '...'
    call system('git clone --depth 1 ' . a:plugin_source_url . ' ' . l:plugin_dest_dir)
    echo 'Installed ' . a:plugin_name . '!'
    echo ' '
  endif
endfunction

command! PlugClean call system('rm -rf ' . s:plugin_dir) | echo 'Removed all plugins'
command! PlugCount echo 'Plugin count: ' . s:CountDirectories(s:plugin_dir)

" Plugins

call s:InstallPlugin('https://github.com/tpope/vim-fugitive', 'vim-fugitive')

set runtimepath+=~/.fzf
nnoremap <C-p> :FZF<CR>

call s:InstallPlugin('https://github.com/tpope/vim-eunuch', 'vim-eunuch')
