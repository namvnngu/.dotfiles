"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" OPTIONS                                                                     "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

syntax on
set notermguicolors
colorscheme memono

let mapleader = ' '

filetype plugin indent on

set shiftwidth=2
set expandtab
set smarttab
set softtabstop=2
set tabstop=4

set clipboard=unnamed,unnamedplus
set colorcolumn=80
set completeopt=menu,menuone,popup,noselect
set cursorline
set grepformat=%f:%l:%c:%m
set grepprg=rg\ --vimgrep
set hidden
set hlsearch
set incsearch
set laststatus=2
set noswapfile
set nowrap
set number
set relativenumber
set ruler
set scrolloff=8
set wildmenu
set wildmode=longest:full
set wildoptions=pum,tagfile

" Avoid using old regexp engine which will incur performance issues in 
" TypeScript syntax highlighting.
set regexpengine=0 

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" KEY MAPPINGS                                                                 "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Move to window
nnoremap <Leader>h <C-W>h
nnoremap <Leader>j <C-W>j
nnoremap <Leader>k <C-W>k
nnoremap <Leader>l <C-W>l

" Yank the current file path
nnoremap yp <Cmd>call setreg('+', expand('%:p'))<CR>

" Yank til the end
nnoremap Y y$

" Clear highlighting search
nnoremap <silent> <C-L> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>

" Keep searching centered
nnoremap n nzz
nnoremap N Nzz

" Keep half-window scrolling centered
nnoremap <C-D> <C-D>zz
nnoremap <C-U> <C-U>zz

" Navigate quickfix list and keep it centered
nnoremap <C-K> <Cmd>cprev<CR>zz
nnoremap <C-J> <Cmd>cnext<CR>zz

" Replace currently selected text with black hole register without yanking it
vnoremap <Leader>p "_dP

" Replace word under cursor
nnoremap <Leader>s :%s/\<<C-R><C-W>\>/<C-R><C-W>/gI<Left><Left><Left>

" Create or edit file
nnoremap <Leader>e :e <C-R>=expand('%:p:h') . '/'<CR>

" Make the current buffer a scratch buffer
nnoremap <Leader>bs <Cmd>setlocal bt=nofile bh=wipe noswf<CR>

" Set a new undo point before deleting text in insert mode
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>

" Switch between C source and header file
nnoremap <Leader>c <Cmd>e %:p:s,.h$,.X123X,:s,.c$,.h,:s,.X123X$,.c,<CR>

" Format the entire file
function s:format()
  let l:view = winsaveview()
  keepjumps keeppatterns silent! norm gggqG
  call winrestview(l:view)
endfunction
nnoremap <Leader>gq <Cmd>call <SID>format()<CR>

" Fix the indentation of the entire file
function s:indent()
  let l:view = winsaveview()
  keepjumps keeppatterns silent! norm gg=G
  call winrestview(l:view)
endfunction
nnoremap <Leader>= <Cmd>call <SID>indent()<CR>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ABBREVIATIONS                                                                "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

ia <expr> me:: strftime('Author: Nam Nguyen <vnngucs@outlook.com><CR>
                       \Description:<CR>
                       \License: The Unlicense<CR>
                       \Date Created: %B %d, %Y<CR>
                       \Date Modified: %B %d, %Y')

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" AUTOCOMMANDS                                                                 "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

augroup mkdir_on_edit
  autocmd!
  autocmd BufWritePre * call s:mkdir_on_edit(expand('<afile>:p:h'))

  function! s:mkdir_on_edit(dir)
    if a:dir =~ '^/'
          \ && !isdirectory(a:dir)
          \ && input("'" . a:dir . "' does not exist. Create? [y/N] ") =~? '^y\%[es]$'
      call mkdir(a:dir, 'p')
    endif
  endfunction
augroup END

augroup c_ft
  autocmd!
  autocmd FileType c,cpp     setlocal tags=./tags,tags,~/.tags/c-system
  autocmd FileType c,cpp     if executable('clang-format') | setlocal formatprg=clang-format | endif
  autocmd BufEnter *.c,*.cpp command! Run :vs | term cc -o %:p:r % && %:p:r
augroup END

augroup markdown_ft
  autocmd!
  autocmd FileType markdown setlocal textwidth=80
augroup END

augroup typst_ft
  autocmd!
  autocmd FileType typst setlocal textwidth=80
augroup END

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" COMMANDS                                                                     "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

command DiffOrig vert new | set bt=nofile | r ++edit # | 0d_
      \ | diffthis | wincmd p | diffthis

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" PLUGINS                                                                      "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" netrw
let g:loaded_netrw       = 1
let g:loaded_netrwPlugin = 1

" man page
runtime ftplugin/man.vim
