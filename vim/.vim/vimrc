"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" OPTIONS                                                                     "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let mapleader = ' '

syntax off
filetype plugin indent on
colorscheme wildcharm

if exists("$TMUX")
  set background=light
  if $THEME_MODE == 'dark'
    set background=dark
  endif
endif

set autoindent
set autoread
set backspace=indent,eol,start
set breakindent
set clipboard+=unnamedplus,unnamed
set colorcolumn=80
set complete-=i
set completeopt=menu,menuone,popup,noinsert,noselect
set copyindent
set cursorline
set display+=lastline,truncate
set expandtab
set foldlevel=99
set foldlevelstart=99
set formatoptions+=j
set grepformat=%f:%l:%c:%m
set grepprg=rg\ --vimgrep
set history=10000
set hlsearch
set ignorecase
set incsearch
set laststatus=2
set linebreak
set nobackup
set nolangremap
set noswapfile
set notermguicolors
set noundofile
set nowrap
set nrformats-=octal
set number
set omnifunc=syntaxcomplete#Complete
set preserveindent
set relativenumber
set ruler
set scrolloff=8
set sessionoptions-=options
set shiftround
set shiftwidth=2
set shortmess=ltToOCF
set showtabline=0
set sidescroll=1
set sidescrolloff=2
set signcolumn=yes
set smartcase
set smartindent
set smarttab
set smoothscroll
set splitbelow
set splitright
set tabstop=2
set ttimeout ttimeoutlen=50
set undolevels=10000
set viewoptions-=options
set viminfo^=!
set virtualedit=block
set wildmenu
set wildmode=longest:full,full
set wildoptions=pum,tagfile
set winminwidth=5

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" MAPPINGS                                                                    "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Avoid using arrow keys
noremap <Up> <NOP>
noremap <Down> <NOP>
noremap <Left> <NOP>
noremap <Right> <NOP>

" Move to window
nnoremap <leader>h :wincmd h<CR>
nnoremap <leader>j :wincmd j<CR>
nnoremap <leader>k :wincmd k<CR>
nnoremap <leader>l :wincmd l<CR>

" Yanking
nnoremap <leader>ya :%y+<CR>
nnoremap <leader>y "+y
vnoremap <leader>y "+y
nnoremap <leader>Y "+y$
nnoremap Y y$
nnoremap yp :exec setreg('+', expand('%:p'))<CR>

" Better indenting
vnoremap < <gv
vnoremap > >gv

"  Move lines
vnoremap J :m '>+1<CR>gv=gv
vnoremap K :m '<-2<CR>gv=gv

" Clear highlighting search
nnoremap <silent> <C-L> :nohlsearch<C-R>=has('diff')
                        \ ? '<Bar>diffupdate'
                        \ : ''<CR><CR><C-L>

" Set a new undo point before joining lines
nnoremap J mzJ`z

" Keeping it centered and stable
nnoremap n nzzzv
nnoremap N Nzzzv
nnoremap <C-D> <C-D>zz
nnoremap <C-U> <C-U>zz

" Quickfix list
nnoremap <C-J> :cnext<CR>zz
nnoremap <C-K> :cprev<CR>zz

" Location list
nnoremap <leader>J :lnext<CR>zz
nnoremap <leader>K :lprev<CR>zz

" Toggle spelling checker
map <F12> :setlocal spell! spelllang=en_us<CR>

" Replace currently selected text with black hole register without yanking it
xnoremap <leader>p "_dP

" Delete without yanking
nnoremap <leader>d "_d
vnoremap <leader>d "_d

" Cancel
inoremap <C-C> <Esc>

" Replace word under cursor
nnoremap <leader>s :%s/\<<C-R><C-W>\>/<C-R><C-W>/gI<Left><Left><Left>

" Make file executable
nnoremap <silent> <leader>x :!chmod +x %<CR>

" Switch file
nnoremap <leader>e :e <C-R>%

" Open a file name under cursor in a new vertical split
nnoremap <leader><C-W>F :vertical wincmd F<CR>

" Exit terminal
tnoremap <C-D> <C-\><C-N>:q!<CR>

" Switch between C source and header file
nnoremap <F5> :e %:p:s,.h$,.X123X,:s,.c$,.h,:s,.X123X$,.c,<CR>

" Fix the indentation of an entire file
nnoremap <leader>F gg=G''

" Set a new undo point before deleting text in insert mode
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>

" File explorer
nnoremap - :Ex<CR>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ABBREVIATIONS                                                               "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

ia <expr> me:: strftime('Author: Nam Nguyen <vnngucs@outlook.com><CR>
                       \Date: %d %b %Y<CR>
                       \License: The Unlicense')

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" AUTOCOMMANDS                                                                "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

augroup trim_white_space
  autocmd!
  autocmd BufWritePre * call s:trim_white_space()
augroup END
function s:trim_white_space()
  let l:save = winsaveview()
  keepjumps keeppatterns silent! %s/\s\+$//e
  keepjumps keeppatterns silent! %s/\%^\n\+//
  keepjumps keeppatterns silent! %s/\(\n\n\)\n\+/\1/
  keepjumps keeppatterns silent! %s/\($\n\s*\)\+\%$//
  call winrestview(l:save)
endfunction

augroup lang_tags
  autocmd!
  autocmd BufReadPre * set tags=./tags,tags
augroup END
augroup c_tags
  autocmd!
  autocmd BufReadPre *.c,*.h call s:set_tags('c')
augroup END
function s:set_tags(lang)
  let l:files = globpath('~/.tags/' . a:lang, '*', 0, 1)
  if len(l:files) != 0
    let &tags = './tags,tags,' . join(l:files, ',')
  endif
endfunction

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" COMMANDS                                                                    "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

command! Ctags !ctags -R .

command! -nargs=1 Cht :sp | enew | .!curl -s "cht.sh/<args>?T"

command DiffOrig vert new | set bt=nofile | r ++edit # | 0d_
      \ | diffthis | wincmd p | diffthis

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" PLUGINS                                                                     "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let s:PLUGIN_ROOT = expand('~/.vim/pack/plugins/start')
let s:PLUGIN_URLS = [
      \ 'https://github.com/tpope/vim-fugitive',
      \ 'https://github.com/junegunn/fzf',
      \ 'https://github.com/ludovicchabant/vim-gutentags',
      \ ]

function s:install_plugins()
  let l:install_urls = ''
  let l:installing_msg = ''
  let l:installed_msg = ''

  for url in s:PLUGIN_URLS
    let l:name = fnamemodify(url, ':t')
    let l:path = expand(s:PLUGIN_ROOT . '/' . l:name)

    if !isdirectory(l:path)
      let l:install_urls = l:install_urls . '\n' . url
      let l:installing_msg = l:installing_msg . "\nInstalling " . l:name . '...'
      let l:installed_msg = l:installed_msg . "\nInstalled " . l:name . '!'
    endif
  endfor

  if !empty(l:install_urls)
    echo trim(l:installing_msg)

    call system("echo -e '" . trim(l:install_urls) . "' "
             \. "| xargs -P8 -I{} sh -c "
             \. "'git clone --depth=1 --filter=blob:none {} "
             \. s:PLUGIN_ROOT . "/$(basename {})'")

    echo trim(l:installed_msg)
  endif
endfunction

command! Pc echo 'Plugin count: ' . len(s:PLUGIN_URLS)
command! Pu call system('rm -rf ' . s:PLUGIN_ROOT)
        \|  call s:install_plugins()
        \|  echo 'Updated all plugins. Restart Vim to get latest updates.'
command! Pl for url in s:PLUGIN_URLS
        \|    echo fnamemodify(url, ':t') . ': ' . url
        \|  endfor

call s:install_plugins()

" fzf
nnoremap <C-P> :FZF<CR>

" matchit.vim
runtime! macros/matchit.vim

" man
runtime ftplugin/man.vim
